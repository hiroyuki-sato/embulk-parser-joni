plugins {
    id "com.jfrog.bintray" version "1.1"
    id "java"
    id "checkstyle"
    id "maven-publish"
    id "org.embulk.embulk-plugins" version "0.4.2"
}
repositories {
    mavenCentral()
    jcenter()
}

group = "com.github.hiroyuki-sato"
version = "0.2.0-SNAPSHOT"
description = "The Embulk parser plugin for the Fluentd regex parser(Oniguruma) compatible."

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compileOnly "org.embulk:embulk-api:0.10.30"
    compileOnly "org.embulk:embulk-spi:0.10.30"
    
    compile  "org.jruby.joni:joni:2.1.41"
    compile  "org.jruby.jcodings:jcodings:1.0.55"
    compile("org.embulk:embulk-util-config:0.2.1") {
        // They conflict with embulk-core. They are once excluded here,
        // and added explicitly with versions exactly the same with embulk-core:0.10.19.
        exclude group: "com.fasterxml.jackson.core", module: "jackson-annotations"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-core"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-databind"
        exclude group: "com.fasterxml.jackson.datatype", module: "jackson-datatype-jdk8"
        exclude group: "javax.validation", module: "validation-api"
    }
    compile "org.embulk:embulk-util-timestamp:0.2.1"
    compile('org.embulk:embulk-util-json:0.1.0') {
        exclude group: "org.msgpack", module: "msgpack-core"
    }
    compile "org.embulk:embulk-util-text:0.1.0"

    // TODO
    // embulk-util-text:0.1.0 depends on embulk-util-file:0.1.0
    // But, embulk-util-file:0.1.0 has a problem using Embulk v0.9
    // So, It needs to write the library version explicitly.
    compile "org.embulk:embulk-util-file:0.1.3"

    // They are once excluded from transitive dependencies of other dependencies,
    // and added explicitly with versions exactly the same with embulk-core:0.10.19.
    compile "com.fasterxml.jackson.core:jackson-annotations:2.6.7"
    compile "com.fasterxml.jackson.core:jackson-core:2.6.7"
    compile "com.fasterxml.jackson.core:jackson-databind:2.6.7"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.6.7"
    compile "javax.validation:validation-api:1.1.0.Final"

    testCompile "junit:junit:4.+"
    testCompile "org.embulk:embulk-api:0.10.30"
    testCompile "org.embulk:embulk-spi:0.10.30"
    testCompile "org.embulk:embulk-core:0.10.30"
    testCompile "org.embulk:embulk-deps:0.10.30"
    testCompile "org.embulk:embulk-core:0.10.30"
    testCompile  "org.embulk:embulk-core:0.10.30:tests"
}

checkstyle {
    configFile = file("${project.rootDir}/config/checkstyle/checkstyle.xml")
    toolVersion = '6.14.1'
}
checkstyleMain {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = true
}
checkstyleTest {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = true
}
task checkstyle(type: Checkstyle) {
    classpath = sourceSets.main.output + sourceSets.test.output
    source = sourceSets.main.allJava + sourceSets.test.allJava
}

embulkPlugin {
    mainClass = "org.embulk.parser.joni_regexp.JoniRegexpParserPlugin"
    category = "parser"
    type = "joni_regexp"
}

publishing {
    publications {
        embulkPluginMaven(MavenPublication) {  // Publish it with "publishEmbulkPluginMavenPublicationToMavenRepository".
            from components.java  // Must be "components.java". The dependency modification works only for it.
        }
    }
    repositories {
        maven {
            url = "${project.buildDir}/mavenPublishLocal"
        }
    }
}

gem {
    from("LICENSE.txt")  // Optional -- if you need other files in the gem.
    authors = [ "Hiroyuki Sato" ]
    email = [ "hiroysato@gmail.com" ]
    // "description" of the gem is copied from "description" of your Gradle project.
    summary = "A Regular expression(Joni) parser plugin for Embulk(Fluentd compatible)"
    homepage = "https://github.com/hiroyuki-sato/embulk-parser-joni_regexp"
    licenses = [ "MIT" ]
//    metadata = [  // Optional -- if you need metadata in the gem.
//        "foo": "bar"
//    ]

    generateRubyCode = false  // Avoiding generate lib/embulk/parser/joni_regexp.rb
    into("lib/embulk/parser/") {
      from "lib/embulk/parser/joni_regexp.rb"
    }
    into("lib/embulk/guess/") {
      from "lib/embulk/guess/joni_regexp.rb"
    }
}

gemPush {
    host = "https://rubygems.org"
}

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked"
}
